prototype(Shel.Neos.Orphanage:Component.NodeTable.Row) < prototype(Neos.Fusion:Component) {
    orphanNode = null

    @private {
        removeAction = Neos.Fusion:ActionUri {
            action = 'removeOrphanNode'
            format = 'htmx'
            arguments {
                nodePath = ${props.orphanNode.path}
                workspaceName = 'live'
            }
        }
        removeOrphanPopoverId = 'remove-orphan-modal'
    }

    renderer = afx`
        <tr id={'orphan-row-' + props.orphanNode.identifier}>
            <td>{props.orphanNode.path}</td>
            <td>{props.orphanNode.workspace}</td>
            <td>{props.orphanNode.nodeType}</td>
            <td>{props.orphanNode.childNodes}</td>
            <td>
                <button
                    class='neos-button neos-button-danger'
                    type='button'
                    title='Remove node'
                    hx-get={private.removeAction}
                    hx-target='#popover-container'
                    hx-swap='innerHTML'
                    hx-on--after-request={'document.getElementById("' + private.removeOrphanPopoverId + '").showPopover()'}
                >
                    <i class='fas fa-trash icon-white'></i>
                </button>
                <button
                    class='neos-button neos-button-danger'
                    type='button'
                    title='Adopt node'
                    popovertarget={'orphan-adopt-' + props.orphanNode.path + '-' + props.orphanNode.workspace}
                >
                    <i class='fas fa-hands-helping icon-white'></i>
                </button>
                <Shel.Neos.Orphanage:Component.Modal.AdoptOrphan orphanNode={props.orphanNode} />
            </td>
        </tr>
    `
}
