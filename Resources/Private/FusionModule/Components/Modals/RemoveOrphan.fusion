prototype(Shel.Neos.Orphanage:Component.Modal.RemoveOrphan) < prototype(Neos.Fusion:Component) {
    orphanNode = null

    @private {
        popoverId = ${'orphan-remove-' + props.orphanNode.path + '-' + props.orphanNode.workspace}
    }

    renderer = afx`
        <div popover id={private.popoverId}>
            <header>
                <button
                    type="button"
                    class="neos-close neos-button"
                    popovertarget={private.popoverId}
                    popovertargetaction="close"
                >
                    <i class="fas fa-times"></i>
                </button>
                <div class="neos-header">
                    Do you really want to delete the node "{props.orphanNode.path}" and its child nodes?
                </div>
            </header>
            <section>
                <p>
                    This will remove the node and its child nodes. This operation cannot be undone.
                </p>
            </section>
            <footer>
                <button
                    type="button"
                    class="neos-button"
                    popovertarget={private.popoverId}
                    popovertargetaction="close"
                >
                    Cancel
                </button>
                <Neos.Fusion.Form:Form
                    form.target.action="removeOrphanNode"
                    form.target.format="htmx"
                    attributes.class="neos-inline"
                    attributes.hx-post={form.getTarget()}
                    attributes.hx-target="closest tr"
                    attributes.hx-swap="delete"
                    attributes.hx-disabled-elt="find button"
                    attributes.hx-indicator="#indicator"
                    attributes.hx-include="[name='moduleArguments[nodeTypeFilter]']"
                >
                    <Neos.Fusion.Form:Hidden
                        field.name="nodePath"
                        field.value={orphanNode.path}
                    />
                    <Neos.Fusion.Form:Hidden
                        field.name="workspaceName"
                        field.value={orphanNode.workspace}
                    />
                    <Neos.Fusion.Form:Button attributes.class="neos-button neos-button-danger">
                        Yes, remove the node
                    </Neos.Fusion.Form:Button>
                </Neos.Fusion.Form:Form>
            </footer>
        </div>
    `
}
