prototype(Shel.Neos.Orphanage:Views.Index) < prototype(Neos.Fusion:Component) {
    @propTypes {
        @strict = true
        orphanNodes = ${PropTypes.arrayOf(PropTypes.instanceOf('Neos\ContentRepository\Domain\Model\NodeInterface')).required}
        totalOrphanNodes = ${PropTypes.integer.required}
        filterableNodeTypes = ${PropTypes.arrayOf(PropTypes.string).required}
        currentPage = ${PropTypes.integer.required}
        pageCount = ${PropTypes.integer.required}
        nodeTypeFilter = ${PropTypes.string}
    }

    currentPage = 1
    pageCount = 1
    totalOrphanNodes = 0
    orphanNodes = ${[]}
    filterableNodeTypes = ${[]} // e.g. ['Neos.Neos:Document', 'Neos.Neos:Content']
    nodeTypeFilter = ''

    renderer = Neos.Fusion:Match {
        @subject = ${request.format}
        @default = afx`
            <section
                hx-indicator="#indicator"
                hx-boost="true"
            >
                <Shel.Neos.Orphanage:Component.HTMXConfig />
                <Shel.Neos.Orphanage:Component.FlashMessages flashMessages={flashMessages}/>
                <Shel.Neos.Orphanage:Component.Indicator />
                <div
                    class="neos-content neos-container-fluid"
                    hx-vals={'{"moduleArguments[currentPage]": ' + props.currentPage + '}'}
                    hx-include="[name='moduleArguments[nodeTypeFilter]']"
                >
                    <div class="neos-row-fluid">
                        <Shel.Neos.Orphanage:Component.NodeTypeFilter
                            nodeTypeFilter={props.nodeTypeFilter}
                            filterableNodeTypes={props.filterableNodeTypes}
                        />
                        <Shel.Neos.Orphanage:Component.NodeTable
                            orphanNodes={props.orphanNodes}
                            totalOrphanNodes={props.totalOrphanNodes}
                        />
                    </div>
                </div>
                <Shel.Neos.Orphanage:Component.Pagination
                    pageCount={props.pageCount}
                    currentPage={props.currentPage}
                />
                <div class="neos-footer">
                    <button
                        class="neos-button neos-button-danger"
                        type="button"
                        title="Remove node"
                        popovertarget="remove-selected-orphans-modal"
                        @if={props.totalOrphanNodes > 0}
                    >
                        <i class="fas fa-trash icon-white"></i> Remove all currently filtered nodes
                    </button>
                </div>
            </section>
            <section id="popover-container"></section>
            <Shel.Neos.Orphanage:Component.Modal.RemoveSelectedOrphans />
        `
        htmx = afx`
            <Shel.Neos.Orphanage:Component.NodeTable
                orphanNodes={orphanNodes}
                totalOrphanNodes={totalOrphanNodes}
            />
            <Shel.Neos.Orphanage:Component.Pagination
                pageCount={props.pageCount}
                currentPage={props.currentPage}
            />
        `
    }
}
